apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "org.quanqi.android.apkpatcher"
        minSdkVersion 8
        targetSdkVersion 21
        versionCode 4
        versionName '1.2'

        ndk {
            moduleName "libbz"
        }

        sourceSets.main {
            jniLibs.srcDir 'src/main/libs'
            // use the jni .so compiled from the manual ndk-build command
            jni.srcDirs = [] //disable automatic ndk-build call
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile 'com.android.support:support-v4:21.0.2'
    compile 'com.alibaba:fastjson:1.2.2'
    compile 'org.apache.commons:commons-lang3:3.3.2'
}


task ndkBuild(type: Exec) {
    def ndkDir = plugins.getPlugin('com.android.application').sdkHandler.ndkFolder
    def ndkCmd = '' + ndkDir + '/ndk-build'
    commandLine ndkCmd , '-C', file('src/main/jni').absolutePath
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}
